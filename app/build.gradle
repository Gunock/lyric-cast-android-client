/*
 * Created by Tomasz Kiljanczyk on 03/01/2022, 23:17
 * Copyright (c) 2022 . All rights reserved.
 * Last modified 03/01/2022, 23:13
 */


plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.dagger.hilt.android'
    id 'com.google.protobuf'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}


android {
    def major = 0
    def minor = 1
    def patch = 6


    signingConfigs {
        release {
            storeFile file(var)
            storePassword 'eAdmin.1@2'
            keyAlias 'LyricCast-release'
            keyPassword 'eAdmin.1@2'
        }
    }

    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        applicationId "pl.gunock.lyriccast"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode major * 100000000 + minor * 10000 + patch
        versionName "$major.$minor.$patch"

        testInstrumentationRunner "pl.gunock.lyriccast.HiltTestRunner"
        resourceConfigurations += ['en', 'pl']
    }

    buildTypes {
        debugMini {
            initWith buildTypes.debug

            minifyEnabled true
            shrinkResources false
            proguardFiles(
                    getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            )

            ndk {
                debugSymbolLevel 'FULL'
            }
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles(
                    getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            )

            ndk {
                debugSymbolLevel 'FULL'
            }
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    testOptions {
        animationsDisabled = true

        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }
    namespace 'pl.gunock.lyriccast'
}

dependencies {
    // Submodules
    implementation project(':common')
    implementation project(':dataTransfer')
    implementation project(':dataModel')

    // App dependencies
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"


    // Architecture Components
    implementation "androidx.datastore:datastore:$datastoreVersion"
    implementation "com.google.protobuf:protobuf-javalite:$protobufJavaLiteVersion"

    // AndroidX
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.core:core-ktx:$androidXCoreVersion"
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "androidx.preference:preference-ktx:$preferenceVersion"
    implementation "androidx.recyclerview:recyclerview:$recyclerViewVersion"
    implementation "androidx.recyclerview:recyclerview-selection:$recyclerViewSelectionVersion"
    implementation "androidx.navigation:navigation-fragment-ktx:$navVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navVersion"

    // AdMob
    implementation "com.google.android.gms:play-services-ads:$googleAdsVersion"

    // Chromecast
    implementation "com.google.android.gms:play-services-cast-framework:$castFrameworkVersion"
    implementation "androidx.mediarouter:mediarouter:$mediaRouterVersion"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    kapt "com.google.dagger:hilt-compiler:$hiltVersion"

    // Firebase
    implementation platform("com.google.firebase:firebase-bom:$firebaseVersion")
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    // AndroidX Test - Instrumented testing
    androidTestImplementation "androidx.test:core-ktx:$androidXTestCoreVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidXTestExtKotlinRunnerVersion"
    androidTestImplementation "androidx.test.ext:junit-ktx:$androidXTestExtKotlinRunnerVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"


    // AndroidX Test - Hilt testing
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hiltVersion"
    kaptAndroidTest "com.google.dagger:hilt-compiler:$hiltVersion"

    // LeakCanary
//    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
}

kapt {
    correctErrorTypes true
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobufJavaLiteVersion"
    }

    // Generates the java Protobuf-lite code for the Protobufs in this project. See
    // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
    // for more information.
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}